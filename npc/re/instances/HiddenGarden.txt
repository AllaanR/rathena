//===== rAthena Script =======================================
//= Hidden Flower Garden
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.2 Hidden Flower Garden
//= Episode 17.2 Security Area 1 & 2
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//============================================================

1@herbs,1,1,0	script	#hidden_flower_garden_control	-1,{
	end;
	
OnInstanceInit:
	set_instance_var("map$",instance_mapname("1@herbs"));
	instance_enable("#172_hgd_guide_1",false);
	instance_enable("#172_hgd_carota",false);
	instance_enable("#172_hgd_guide_2",false);
	instance_enable("#172_pimp25",false);
	instance_enable("#172_hgd_guide_3",false);
	instance_enable("#172_hgd_guide_4",false);
	instance_enable("#172_hgd_sigma",false);
	instance_enable("#172_hgd_lambda",false);
	instance_enable("#172_hgd_kappa",false);
	instance_enable("#172_hgd_varmundt",false);
	for(.@i = 0; .@i < 3; .@i++){
		instance_enable("#172_hgd_guard_" + .@i,false);
		instance_enable("#172_hgd_eff_" + .@i,false);
	}
	instance_enable("#172_sec_guide",false);
	instance_enable("#172_sec_kappa",false);
	instance_enable("#172_sec_lambda",false);
	instance_enable("#172_sec_sigma",false);
	for(.@i = 0; .@i < 4; .@i++)
		instance_enable("#172_sec_warp_" + .@i,false);
	instance_enable("#172_sec_warp_n",false);
	instance_enable("#172_sec_warp_h",false);
end;
}

1@herbs,56,278,3	script	#172_hgd_guide_0	4_SYS_MSG,{
	.@var = get_instance_var("hgd");
	if(!.@var){
		mes "It appears to be a hologram of an automatic doll. You can see a strange glass on the middle panel with the word ^0000FF[Enter the pattern on the touch screen]^000000 and other several buttons.";
		next;
		while(true){
			switch(select("Leave it alone","Touch the screen",(!.@retry) ? "":"^FF0000Try entering a different pattern^000000")){
				case 1:
					mes "The hologram faded.";
					end;
					
				case 2:
				case 3:
					break;
			}
			specialeffect EF_AGIUP2;
			progressbar_npc "3131FF",1;
			if(.@retry) break;
			if(rand(1,5) > 1){
				specialeffect EF_HALLUTIME;
				mes "^FF0000ERROR^000000";
				mes "^FF0000Pattern does not match.^000000";
				next;
				.@retry = true;
				continue;
			} else
				break;
		}
		specialeffect EF_GUARD4;
		mes "^0000FFPattern recognized.^000000";
		mes "Running the sleep mode release protocol. Pattern input by ^0000CD" + strcharinfo(0) + "^000000 has been recognized.";
		close2;
		setpcblock PCBLOCK_ALL,true;
		npctalk "This product is an automatic doll that serves as a guide for the flower garden with a serial number BMD000001.";
		sleep2 5000;
		npctalk "The similarity check to Varmundt's mana pattern is at 73.6%. Switching to flower garden guide mode.";
		sleep2 5000;
		npctalk "Operation will begin in 3 seconds.";
		progressbar_npc "3131FF",3;
		setnpcdisplay instance_npcname(strnpcinfo(0)),20622;
		specialeffect EF_FLOWERCAST;
		npctalk "Sleep mode release protocol has been completed. I will check if the master, Varmundt is nearby in addition to the recognized target.";
		sleep2 5000;
		npctalk "Searching......";
		progressbar_npc "3131FF",3;
		sleep2 1000;
		npctalk "Master Varmundt is not detected within the network.";
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		set_instance_var("hgd",1);
		set_instance_var("admin$",strcharinfo(0));
		setpcblock PCBLOCK_ALL,false;
		end;
	}
	if(.@var == 1){
		.@admin$ = get_instance_var("admin$");
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		setpcblock PCBLOCK_ALL,true;
		npctalk "I'm an automatic doll that was designed to work only if your mana synchronizes with Varmundt's mana over a certain level.";
		sleep2 5000;
		npctalk "[" + .@admin$ + "]" + " has a 73.6% mana similarity to that of Varmundt. Who exactly are you [" + .@admin$ + "]?";
		sleep2 5000;
		setpcblock PCBLOCK_ALL,false;
		select("An adventurer who stumbled upon this place");
		setpcblock PCBLOCK_ALL,true;
		npctalk "It's strange. I've never seen an outsider with a mana pattern that matches more than 70% to that of Varmundt's.";
		sleep2 5000;
		setpcblock PCBLOCK_ALL,false;
		if(select("I had the opportunity to meet Varmundt before.:I don't know why.") == 1){
			sleep2 1000;
			npctalk "There is a possibility that you have a similar mana pattern due to frequent contact with him in any form.";
			sleep2 5000;
		} else
			sleep2 1000;
		setpcblock PCBLOCK_ALL,true;
		while(!.@s){
			npctalk "Okay. From now on, I will set [" + .@admin$ + "] as our temporary administrator and take action accordingly. Do you agree?";
			sleep2 5000;
			setpcblock PCBLOCK_ALL,false;
			switch(select("I'm a temporary administrator? Why should I?:Refuse!:Sure")){
				case 1:
					setpcblock PCBLOCK_ALL,true;
					sleep2 1000;
					npctalk "The automatic dolls maintain the flower garden and perform their given duties individually.";
					sleep2 5000;
					npctalk "While Master Varmundt was still present, the progress of duties was checked in the form of individual reports and weekly on-site evaluations.";
					sleep2 5000;
					npctalk "Currently, Master Varmundt has been away for over 1 million hours.";
					sleep2 5000;
					npctalk "Although you are not Master Varmundt, as someone who has more than 70% similarity to his mana pattern, I would like to ask you to evaluate our duties.";
					sleep2 5000;
					npctalk "Please understand that it's a necessary management to prevent our duties from falling into the wrong path.";
					setpcblock PCBLOCK_ALL,false;
					break;
					
				case 2:
					sleep2 1000;
					npctalk "I will wait until you give me your permission.";
					specialeffect EF_MARKING_USE_CHANGEMONSTER;
					setpcblock PCBLOCK_ALL,false;
					end;
					
				case 3:
					setpcblock PCBLOCK_ALL,false;
					.@s = true;
					break;
			}
			if(.@s) break;
		}
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "Thank you for your permission. From now on, I'll treat [" + .@admin$ + "] as a temporary administrator.";
		sleep2 5000;
		npctalk "First select a code name that will be used to control this automatic doll. Some of the names that were preferred in the past are randomly selected.";
		setarray .@names$,"Lisa","Barbara","Jennifer","Dorothy","Jun","Yeongchun","Susan","Martha","Hannah","Helen","Chunja","Karen","How do I stop this?";
		.@size = getarraysize(.@names$);
		.@m$[0] = "Quit";
		for(.@i = 1; .@i < 4; .@i++){
			.@name$ = .@names$[rand(.@size)];
			while(inarray(.@m$,.@name$) != -1)
				.@name$ = .@names$[rand(.@size)];
			.@m$[.@i] = .@name$;
		}
		.@menu$ = implode(.@m$,":");
		setpcblock PCBLOCK_ALL,false;
		.@s = select(.@menu$) - 1;
		setpcblock PCBLOCK_ALL,true;
		set_instance_var("guide$",.@m$[.@s]);
		set_instance_var("guide",getnpcid(0,instance_npcname(strnpcinfo(0))));
		npctalk "Order has been received.";
		sleep2 3000;
		npctalk "The code name of the automatic doll BMD000001 is [" + get_instance_var("guide$") + "].";
		sleep2 5000;
		npctalk "We will start the on-site inspection of the garden based on the temporary administrator response protocol.";
		sleep2 5000;
		set_instance_var("hgd",2);
		instance_disable("#172_device_0");
		setpcblock PCBLOCK_ALL,false;
		npctalk "Please watch your step and follow me.";
		npcspeed 200;
		unitwalk get_instance_var("guide"),63,290,instance_npcname(strnpcinfo(0))+"::OnEvent01";
		end;
	}
	if(.@var == 3){
		.@guide$ = get_instance_var("guide$");
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		mes "[ " + .@guide$ + " ]";
		mes "Please evaluate the landscape, water quality, and other overall layout of the garden.";
		next;
		while(true){
			switch(select("Hold on.:It's a beautiful place.:I wish there were more trees.:Why is it surrounded by water?")){
				case 1:
					npctalk "I'll stop the guide. Automatic dolls and objects that has not been inspected and evaluated by the Automatic Doll [" + .@guide$ + "] are not allowed to go outside.";
					end;
					
				case 2:
					npctalk "Then, it has been determined that this place management has met its conditions.";
					break;
					
				case 3:
					set_instance_var("trees",1);
					npctalk "Understood, we will add more trees in the future.";
					break;
					
				case 4:
					npctalk "Because this is a water garden, the area is surrounded by water.";
					sleep2 5000;
					npctalk "Changing to a non-water garden can not be accepted, as it is an area beyond Varmundt's taste.";
					continue;
			}
			break;
		}
		set_instance_var("hgd",4);
		unitwalk get_instance_var("guide"),63,288,instance_npcname(strnpcinfo(0))+"::OnEvent02";
		end;
	}
	end;
		
OnEvent01:
	.@admin$ = get_instance_var("admin$");
	npctalk "You can start the evaluation from the entrance of this garden.";
	sleep 3000;
	npctalk "Objective evaluation reduces the working errors of the automatic dolls that manage this place and helps them perform their duties close to perfection.";
	sleep 6000;
	npctalk "[" + .@admin$ + "]. Please use the authority of the temporary administrator to determine whether the garden duties is being faithfully carried out.";
	sleep 6000;
	set_instance_var("hgd",3);
	specialeffect EF_MARKING_USE_CHANGEMONSTER;
end;

OnEvent02:
	.@admin$ = get_instance_var("admin$");
	if(!get_instance_var("trees")){
		sleep 5000;
		npctalk "We have satisfied the criteria of the temporary administrator [" + .@admin$ + "] and will maintain the current management routine as it is.";
	}
	sleep 5000;
	npctalk "[" + .@admin$ + "], I'd like to request for you to continue the on-site inspection for the remaining sections.";
	unitwalk get_instance_var("guide"),82,300,instance_npcname(strnpcinfo(0))+"::OnEvent03";
end;

OnEvent03:
	.@map$ = get_instance_var("map$");
	npctalk "There is no bridge that connects the sections here.";
	sleep 6000;
	npctalk "Since Varmundt was a person who can move regardless of the terrain, we did not install a separate structure for normal people during the creation of the flower garden.";
	sleep 6000;
	npctalk "However, considering the temporary administrator's insufficient ability, I will support the space leap services.";
	sleep 6000;
	instance_enable("#172_hgd_guide_1",true);
	instance_enable("#172_hgd_carota",true);
	instance_warpall .@map$,111,275,instance_id();
	sleep 1000;
	specialeffect EF_MARKING_USE_CHANGEMONSTER,AREA,instance_npcname("#172_hgd_guide_1");
	instance_disable("#172_hgd_guide_0");
end;
}

1@herbs,56,278,4	duplicate(dummynpc2)	#172_device_0	1_SHADOW_NPC

1@herbs,109,275,3	script	#172_hgd_guide_1	20622,3,3,{
	if(get_instance_var("hgd") == 4){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		set_instance_var("hgd",5);
		npctalk "Master Varmundt gave each of the automatic dolls a research project.";
		sleep 5000;
		npctalk "However, the purpose of the research is not available to us dolls, we have to decide on our own what to do with it in the future.";
		sleep 5000;
		npctalk "The flower garden is divided into several research sectors and each sector has a different subject as a project.";
		npcspeed 200;
		set_instance_var("guide",getnpcid(0,instance_npcname(strnpcinfo(0))));
		unitwalk get_instance_var("guide"),124,275,instance_npcname(strnpcinfo(0))+"::OnEvent00";
	}
	end;
	
OnEvent00:
	unitwalk get_instance_var("guide"),124,281,instance_npcname(strnpcinfo(0))+"::OnEvent01";
end;

OnEvent01:
	unitwalk get_instance_var("guide"),125,280,instance_npcname(strnpcinfo(0))+"::OnEvent02";
	npctalk "Among the research sectors, this sector is a place to grow a plant called Turnip under the orders of Varmundt.";
end;

OnEvent02:
	unitwalk get_instance_var("guide"),137,280,instance_npcname(strnpcinfo(0))+"::OnEvent03";
end;
	
OnEvent03:
	unitwalk get_instance_var("guide"),137,276,instance_npcname(strnpcinfo(0))+"::OnEvent04";	
end;

OnEvent04:
	sleep 3000;
	npctalk "Let me introduce the automatic doll model CAROTA-88.";
	sleep 4000;
	npctalk "......";
	sleep 2000;
	npctalk "[Error: CAROTA-88 is out of control range. Need to track corresponding object coordinates.]";
	sleep 5000;
	npctalk "[Tracking coordinates...]";
	progressbar_npc "3131FF",4;
	npctalk "CAROTA-88. Please return to your original location as soon as possible. Don't forget that I can use force when needed.";
	set_instance_var("carota",getnpcid(0,instance_npcname("#172_hgd_carota")));
	instance_event("#172_hgd_carota","OnAction",false);
end;

OnEvent05:
	npctalk "Let's move on to the next section.",instance_npcname("#172_hgd_guide_1");
	sleep 2000;
	set_instance_var("hgd",7);
	instance_enable("#172_hgd_guide_2",true);
	instance_enable("#172_pimp25",true);
	instance_event("#172_pimp25","OnWalk",false);
	instance_warpall get_instance_var("map$"),254,282,instance_id();
	sleep 1000;
	specialeffect EF_MARKING_USE_CHANGEMONSTER,AREA,instance_npcname("#172_hgd_guide_2");
	instance_disable(strnpcinfo(0));
end;
	
OnTouch:
	if(get_instance_var("hgd") == 4){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
	}
end;
}

1@herbs,137,269,3	script	#172_hgd_carota	CLEAR_NPC,3,3,{
	if(get_instance_var("hgd") == 6){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		select("Report your work details.");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "Master Varmundt. No further instructions after entering Turnip's biological data . This automatic doll unattended.";
		sleep2 5000;
		npctalk "No further indications for 5076 hours. This automatic doll's AI collects network information about turnips.";
		sleep2 5000;
		npctalk "Obtaining information of edible plants. Finding ancient record of turnip cultivation.";
		sleep2 5000;
		npctalk "Optimized germination, growth and sowing of turnips for about 18816 hours compared and recorded.";
		sleep2 5000;
		npctalk "Over million hours passed. Collect data on cultivation and harvest of Turnips. a collection of information on breeding method.";
		sleep2 5000;
		npctalk "2 ton yield per year. 99.9% success pest control prevention. 0.2% loss due to physical work errors.";
		sleep2 5000;
		npctalk "Turnips from flower garden. Sold at an excellent price in a nearby big city. Contributing to maintenance costs for flower gardens and mansions.";
		sleep2 5000;
		npctalk "Whether the research mission direction of this automatic doll should be improved, continued, or discarded. Waiting for the administrator's decision.";
		mes "^0000AAEvaluate CAROTA-88's work to date.^000000";
		next;
		setpcblock PCBLOCK_ALL,false;
		switch(select("The product is full and good shape.:Improve breeding.:It's just farming, so everything is good.")){
			case 1:
			case 3:
				.@talk = true;
				break;
				
			case 2:
				break;
		}
		close2;
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "Evaluation received...";
		sleep2 5000;
		if(.@talk){
			npctalk "CAROTA-88 received confirmation from administrator that Varmundt instructions were performed perfectly.";
			sleep2 3000;
			emotion ET_DELIGHT;
			sleep2 2000;
			npctalk "Existing work routine will be repeated until further orders are issued.";
		} else {
			npctalk "Learning more breeding method.";
			sleep2 3000;
			emotion ET_DELIGHT;
			sleep2 2000;
			npctalk "I will actively increase the portion of experiments on improved product seeds that were to be discarded.";
		}
		sleep2 5000;
		npctalk "Thank you, [" + get_instance_var("admin$") + "] for responding to the evaluation.";
		sleep2 5000;
		if(.@talk)
			npctalk "Automatic doll entering control mode to eradicate underground moles. Goodbye.";
		else
			npctalk "I'll move to the basement storage to check for new breeds.";
		setpcblock PCBLOCK_ALL,false;
		progressbar_npc "3131FF",4;
		instance_disable(strnpcinfo(0));
		npctalk "CAROTA-88 shows a non-logical response to this evaluation. We will arrange for an inspection soon.",instance_npcname("#172_hgd_guide_1");
		sleep 3000;
		unitwalk get_instance_var("guide"),147,278,instance_npcname("#172_hgd_guide_1") + "::OnEvent05";
	}
	end;
	
OnEvent00:
	npctalk "The project of CAROTA-88 is to grow crops here and protect them from outsiders. Why were you away?",instance_npcname("#172_hgd_guide_1");
	unitwalk get_instance_var("carota"),137,272,instance_npcname(strnpcinfo(0))+"::OnEvent01";
end;

OnEvent01:
	.@doll$ = instance_npcname("#172_hgd_guide_1");
	sleep 5000;
	emotion ET_FRET;
	sleep 3000;
	npctalk "Currently, a human administrator is present here. I recommend changing the language code to the official language of humans.",.@doll$;
	sleep 3000;
	emotion ET_FRET;
	sleep 2000;
	progressbar_npc "3131FF",2;
	sleep 1000;
	specialeffect EF_FLOWERCAST;
	sleep 3000;
	npctalk "Management automatic doll irreasonable!! Very!! Forced recall during pursuit of 5 moles!!! The Turnips is ruined!!!";
	sleep 5000;
	npctalk "Haven't you updated your language pack yet? Regular updates are not mandatory, but its essential.",.@doll$;
	sleep 5000;
	npctalk "CAROTA-88 data storage capacity. Language expansion pack burden. If not important, I will return to original work.";
	sleep 5000;
	npctalk "Please wait CAROTA-88. Here is a administrator who can perform the on-site inspection.",.@doll$;
	sleep 5000;
	npctalk "Hasn't it been a long time since CAROTA-88 was last evaluated?",.@doll$;
	sleep 5000;
	npctalk "CAROTA-88 More than a million hours since last research evaluation. Failed to save more logs.";
	sleep 5000;
	npctalk "CAROTA-88. Don't you want a project evaluation? If we miss this administrator now, we don't know when we can get another project evaluation.",.@doll$;
	sleep 5000;
	npctalk "Agree with automatic doll [" + get_instance_var("guide$") + "]. I want an immediate project assessment.";
	sleep 6000;
	npctalk "Then, open your control panel to [" + get_instance_var("admin$") + "]. CAROTA-88.",.@doll$;
	progressbar_npc "3131FF",2;
	sleep 1000;
	specialeffect EF_FLOWERCAST;
	sleep 2000;
	npctalk "Automatic doll CAROTA-88 control panel opened. Waiting for input...";
	set_instance_var("hgd",6);
	specialeffect EF_MARKING_USE_CHANGEMONSTER;
end;

OnAction:
	npcspeed 200;
	switch(get_instance_var("action")){
		case 0: .@x = 138; .@y = 266; .@event$ = "OnAction"; break;
		case 1: .@x = 139; .@y = 272; .@event$ = "OnAction"; break;
		case 2: .@x = 135; .@y = 270; .@event$ = "OnAction"; .@effect = true; break;
		case 3: .@x = 140; .@y = 270; .@event$ = "OnAction"; .@effect = true; break;
		case 4: .@x = 135; .@y = 270; .@event$ = "OnAction"; .@effect = true; break;
		case 5: .@x = 138; .@y = 266; .@event$ = "OnAction"; .@effect = true; break;
		case 6: .@x = 140; .@y = 267; .@event$ = "OnAction"; .@effect = true; break;
		case 7: .@x = 139; .@y = 266; .@event$ = "OnAction"; break;
		case 8: .@x = 135; .@y = 267; .@event$ = "OnAction"; .@effect = true; break;
		case 9: .@x = 139; .@y = 271; .@event$ = "OnAction"; .@transform = true; break;
		case 10: .@x = 134; .@y = 272; .@event$ = "OnAction"; break;
		case 11: .@x = 136; .@y = 270; .@event$ = "OnAction"; break;
		case 12: .@x = 134; .@y = 271; .@event$ = "OnAction"; break;
		case 13: .@x = 134; .@y = 267; .@event$ = "OnEvent00"; break;
	}
	set_instance_var("action",get_instance_var("action") + 1);
	if(.@effect) specialeffect EF_METEORSTORM;
	unitwalk get_instance_var("carota"),.@x,.@y,instance_npcname(strnpcinfo(0))+ "::" + .@event$;
	if(.@transform) setnpcdisplay instance_npcname(strnpcinfo(0)),3153;
end;

OnTouch:
	if(get_instance_var("hgd") == 7){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
	}
end;
}

1@herbs,250,280,4	script	#172_hgd_guide_2	20622,3,3,{
	if(get_instance_var("hgd") == 7){
		set_instance_var("hgd",8);
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		npcspeed 200;
		set_instance_var("guide",getnpcid(0,instance_npcname(strnpcinfo(0))));
		npctalk "This section is where we study a plant called Pumpkin.";
		unitwalk get_instance_var("guide"),253,277,instance_npcname(strnpcinfo(0)) + "::OnEvent01";
	}
	end;
	
OnEvent01:
	unitwalk get_instance_var("guide"),253,265,instance_npcname(strnpcinfo(0)) + "::OnEvent02";
end;

OnEvent02:
	unitwalk get_instance_var("guide"),251,265,instance_npcname(strnpcinfo(0)) + "::OnEvent03";
end;

OnEvent03:
	npctalk "The one in charge of this place is pimp25. An automatic doll modeled after a create from this world.";
	specialeffect EF_MARKING_USE_CHANGEMONSTER,AREA,instance_npcname("#172_pimp25");
	set_instance_var("hgd",9);
end;

OnEvent04:
	unitwalk get_instance_var("guide"),254,254,instance_npcname(strnpcinfo(0)) + "::OnEvent05";
end;

OnEvent05:
	unitwalk get_instance_var("guide"),251,251,instance_npcname(strnpcinfo(0)) + "::OnEvent06";
end;

OnEvent06:
	unitwalk get_instance_var("guide"),239,251,instance_npcname(strnpcinfo(0)) + "::OnEvent07";
end;

OnEvent07:
	npctalk "Let's move on.";
	instance_enable("#172_hgd_guide_3",true);
	for(.@i = 0; .@i < 3; .@i++){
		instance_enable("#172_hgd_guard_" + .@i,true);
		instance_hide("#172_hgd_guard_" + .@i,true);
	}
	sleep 5000;
	instance_warpall get_instance_var("map$"),206,288,instance_id();
	sleep 1000;
	specialeffect EF_MARKING_USE_CHANGEMONSTER,AREA,instance_npcname("#172_hgd_guide_3");
	instance_disable(strnpcinfo(0));
end;
	
OnTouch:
	if(get_instance_var("hgd") == 7){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
	}
end;
}

1@herbs,240,268,2	script	#172_pimp25	2136,3,3,{
	if(!get_instance_var("pimp")){
		set_instance_var("pimp",1);
		npcstop;
	}
	if(get_instance_var("hgd") == 9){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		while(true){
			switch(select("Cancel.:How long have you been in care of this place?:Are they all farming here?")){
				case 1:
					end;
					
				case 2:
					break;
					
				case 3:
					npctalk "My work is to witness the birth and evolution of life! My research is not easy by any means!";
					continue;
			}
			break;
		}
		setpcblock PCBLOCK_ALL,true;
		npctalk "How long has it been since I got the orders of Varmundt?!";
		sleep2 5000;
		npctalk "Probably... about... More than a million hours have passed. Subsequent facility management was determined by self-made decision.";
		sleep2 5000;
		npctalk "Pimp. When you are reporting to the administrator, you must report the exact recorded time.",instance_npcname("#172_hgd_guide_2");
		sleep2 3000;
		npctalk "Is that so? But Pimp can't afford to record more than a million hours. That's a waste of memory resources! Varmundt is angry!";
		sleep2 1000;
		setpcblock PCBLOCK_ALL,false;
		select("How is your research on Pumpkin farming?");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "Plants currently growing. Pumpkin has showed significant results in controlling sodium in human blood vessels.";
		sleep2 5000;
		npctalk "Pumpkin produced here is high in sugar and sufficient calories. It's very delicious according to humans. Nearby dogs and cats also eat it...";
		sleep2 1000;
		setpcblock PCBLOCK_ALL,false;
		select("Is it made of food?");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "I judged it to be a valued highly for various organisms. It's been continously produced, processed, and stored.";
		sleep2 5000;
		npctalk "The current annual output is about three tons. For long-term preservation, it's being stored in warehouses in 2~6 form of dried food .";
		sleep2 1000;
		setpcblock PCBLOCK_ALL,false;
		select("Did you decide all those methods?");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "Pimp has no will to make his own decisions! Only Master Varmundt knows.";
		sleep2 1000;
		setpcblock PCBLOCK_ALL,false;
		select("Is there someone who decided for your research?");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "I asked Varmundt what to do several times, but there was no answer.";
		sleep2 5000;
		npctalk "As there was no orders from the master, Pimp started looking for a place where research could be useful.";
		sleep2 1000;
		setpcblock PCBLOCK_ALL,false;
		select("Have you found it?");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "The maintenance of this mansion costs money. It costs a lot!";
		sleep2 3000;
		npctalk "The dried pumpkins in the warehouses are preserve food for the adventurers and are sold around the continent. Sales becomes money source.";
		sleep2 5000;
		npctalk "Money can protect the mansion. Pimp will protect this place! That's I'm working hard to raise pumpkins!";
		sleep2 1000;
		setpcblock PCBLOCK_ALL,false;
		select("I'm surprised you decided that far on your own.");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "I made a decision? I've never had such inference.";
		sleep2 3000;
		npctalk "......";
		sleep2 2000;
		npctalk "[Calculating]";
		progressbar_npc "3131FF",3;
		set_instance_var("hgd",10);
		setpcblock PCBLOCK_ALL,false;
		npctalk "You confused me! Pimp shall perform the only given task. Don't talk to me anymore administrator.";
		sleep2 5000;
		npctalk "Are you done talking? The conversation here is recorded as evaluation data. Let's head to the next section.",instance_npcname("#172_hgd_guide_2");
		unitwalk get_instance_var("guide"),254,261,instance_npcname("#172_hgd_guide_2")+"::OnEvent04";
		end;
	}
	if(get_instance_var("hgd") > 9)
		npctalk "Get out of here!";
	end;
	
OnWalk:
	npcspeed 150;
	while(!get_instance_var("pimp")){
		switch(get_instance_var("walk")){
			case 0: .@x = 249; .@y = 268; set_instance_var("walk",1); break;
			case 1: .@x = 249; .@y = 259; set_instance_var("walk",2); break;
			case 2: .@x = 240; .@y = 259; set_instance_var("walk",3); break;
			case 3: .@x = 240; .@y = 268; set_instance_var("walk",0); break; 
		}
		if(!get_instance_var("pimp"))
			npcwalkto .@x,.@y;
		sleep 1500;
	}
end;

OnTouch:
	if(get_instance_var("hgd") == 9){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
	}
end;
}

1@herbs,201,287,3	script	#172_hgd_guide_3	20622,3,3,{
	if(get_instance_var("hgd") == 10){
		.@name$ = get_instance_var("guide$");
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		mes "[ " + .@name$ + " ]";
		mes "This section is managed by three automatic dolls. Shall we continue with the guide?";
		next;
		if(select("Cancel.:Let's continue.") == 1){
			mes "[ " + .@name$ + " ]";
			mes "I'll wait until further instructions are given.";
			end;
		}
		set_instance_var("hgd",11);
		npcspeed 200;
		set_instance_var("guide",getnpcid(0,instance_npcname(strnpcinfo(0))));
		npctalk "I don't know what this place is for because it's not accessible to my authority level.";
		unitwalk get_instance_var("guide"),201,285,instance_npcname(strnpcinfo(0)) + "::OnEvent01";
		end;
	}
	if(get_instance_var("hgd") == 12){
		.@admin$ = strcharinfo(0);
		.@name$ = get_instance_var("guide$");
		.@map$ = get_instance_var("map$");
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		mes "[ " + .@name$ + " ]";
		mes "Can I help you with anything?";
		next;
		if(select("Cancel.:What are those automatic dolls?") == 1){
			mes "[ " + .@name$ + " ]";
			mes "Entering standby mode.";
			end;
		}
		mes "[ " + .@name$ + " ]";
		mes "Automatic dolls are classified according to their usage.";
		next;
		mes "[ " + .@name$ + " ]";
		mes "The unit that attacked [" + .@admin$ + "] is the initial version of Class 4 combat-type automatic dolls. It's combat strength is equivalent to 20 adult humans.";
		next;
		mes "[ " + .@name$ + " ]";
		mes "I've delivered the information to the Manager ^0000CDSigma^000000, because the results were unexpected. The temporary administrator's combat capability is not of an average human.";
		next;
		select("Who's that??");
		mes "[ " + .@name$ + " ]";
		mes "It's one of the three most privileged control systems in this mansion.";
		close2;
		while(true){
			setpcblock PCBLOCK_ALL,false;
			.@question$ = (.@talk && .@talk2 ? "^FF0000Is the security area near here?^000000":"");
			.@teleport$ = (.@talk3 > 0 ? "^FF11FFGuide me to the security area.^000000":"");
			switch(select("Is the garden a security area?","What's the real purpose of the garden",.@question$,.@teleport$)){
				case 1:
					sleep2 1000;
					setpcblock PCBLOCK_ALL,true;
					npctalk "The flower garden is the border between the security area and the general area.";
					sleep2 5000;
					npctalk "It is classified as a security zone to those who have enough authority.";
					sleep2 3000;
					.@talk = 1;
					break;
					
				case 2:
					sleep2 1000;
					setpcblock PCBLOCK_ALL,true;
					npctalk "Please understand that this is a buffer zone designed to protect areas that should not be seen by outsiders.";
					sleep2 5000;
					npctalk "Access to any additional information beyond this is prohibited at my authority level.";
					sleep2 3000;
					.@talk2 = 1;
					break;
					
				case 3:
					sleep2 1000;
					setpcblock PCBLOCK_ALL,true;
					npctalk "--- Operating ---";
					progressbar_npc "3131FF",3;
					npctalk "[Warning] This information is close to the limit of the current authority.";
					sleep2 5000;
					npctalk "Security zones is very close to the current coordinates.";
					sleep2 3000;
					.@talk3 = 1;
					break;
					
				case 4:
					setpcblock PCBLOCK_ALL,true;
					npctalk "--- Operating ---";
					progressbar_npc "3131FF",3;
					npctalk "[Warning] This is a request is exceeds my authority level.";
					sleep2 5000;
					npctalk "Your request has been rejected due to a low authority level for the administrator [" + .@admin$ + "].";
					setpcblock PCBLOCK_ALL,false;
					sleep2 5000;
					mapannounce .@map$,"??? : The automatic dolls' response to you is very fresh. Strange visitor.",bc_map,0xF6D6B6;
					sleep2 5000;
					mapannounce .@map$,"??? : Will you escort him to the secure area? BMD000001. No, I mean automatic doll, [" + .@name$ +"].",bc_map,0xF6D6B6;
					sleep2 5000;
					npctalk "The biological wavelength of this voice...";
					sleep2 3000;
					mapannounce .@map$,"It's convenient that I don't have to tell me who I am. Anyway, are you sure you're going to be able to tell me what happened earlier?",bc_map,0xF6D6B6;
					sleep2 5000;
					npctalk "You mean the security area? But it's not possible with my authority level.";
					sleep2 3000;
					mapannounce .@map$,"That's already been taken care of. [" + .@name$ + "], Guide the visitor to this place.",bc_map,0xF6D6B6;
					sleep2 5000;
					npctalk "This is the first time since I was created. That an exception has occurred, we will modify some of the procedures in regards to that.";
					sleep2 5000;
					set_instance_var("hgd",13);
					npctalk "I'll take you to the security area.";
					unitwalk get_instance_var("guide"),196,275,instance_npcname(strnpcinfo(0))+"::OnEvent08";
					end;
			}
		}
	}
	end;
	
OnEvent:
	.@map$ = get_instance_var("map$");
	npctalk "[Searching the coordinates of the automatic doll in the area.]";
	progressbar_npc "3131FF",2;
	mapannounce .@map$,"??? : Unauthorized outsider intrusion detection. Code Category 063. Entering the identification process.",bc_map,0xFF44FF;
	sleep 5000;
	npctalk "It looks like the automatic dolls are back. I don't think it's friendly to the administrator.";
	sleep 5000;
	specialeffect EF_FIREPILLARON2,AREA,instance_npcname("#172_hgd_guard_0");
	sleep 500;
	specialeffect EF_FIREPILLARON2,AREA,instance_npcname("#172_hgd_guard_1");
	sleep 500;
	specialeffect EF_FIREPILLARON2,AREA,instance_npcname("#172_hgd_guard_2");
	instance_hide("#172_hgd_guard_0",false);
	instance_hide("#172_hgd_guard_1",false);
	instance_hide("#172_hgd_guard_2",false);
	setnpcdisplay instance_npcname("#172_hgd_guard_0"),1739;
	setnpcdisplay instance_npcname("#172_hgd_guard_1"),1739;
	setnpcdisplay instance_npcname("#172_hgd_guard_2"),1739;
	sleep 2000;
	npctalk "Facial biometrics. Comparing to list of administrators registered in database.",instance_npcname("#172_hgd_guard_2");
	sleep 2000;
	npctalk "---- No match ----",instance_npcname("#172_hgd_guard_0");
	sleep 500;
	npctalk "---- No match ----",instance_npcname("#172_hgd_guard_1");
	sleep 500;
	npctalk "---- No match ----",instance_npcname("#172_hgd_guard_2");
	sleep 2000;
	npctalk "Suppress unauthorized outsider.",instance_npcname("#172_hgd_guard_0");
	sleep 500;
	npctalk "---- Combat Mode ----",instance_npcname("#172_hgd_guard_1");
	sleep 500;
	npctalk "---- Combat Mode ----",instance_npcname("#172_hgd_guard_2");
	sleep 500;
	for(.@i = 0; .@i < 3; .@i++)
		instance_event("#172_hgd_guard_" + .@i,"OnSummon",false);
end;

OnEvent2:
	npctalk "Let's move to the warp point. Please stay near me.";
	instance_enable("#172_hgd_guide_4",true);
	instance_enable("#172_hgd_sigma",true);
	instance_enable("#172_hgd_lambda",true);
	instance_enable("#172_hgd_kappa",true);
	instance_enable("#172_hgd_varmundt",true);
	instance_hide("#172_hgd_lambda",true);
	instance_hide("#172_hgd_kappa",true);
	instance_hide("#172_hgd_varmundt",true);
	for(.@i = 0; .@i < 3; .@i++){
		instance_enable("#172_hgd_eff_" + .@i,true);
		instance_hide("#172_hgd_eff_" + .@i,true);
	}
	sleep 5000;
	instance_warpall get_instance_var("map$"),87,106,instance_id();
	sleep 1000;
	specialeffect EF_MARKING_USE_CHANGEMONSTER,AREA,instance_npcname("#172_hgd_guide_4");
	instance_disable(strnpcinfo(0));
end;
	
OnEvent01:
	npcspeed 200;
	unitwalk get_instance_var("guide"),201,281,instance_npcname(strnpcinfo(0)) + "::OnEvent02";
end;

OnEvent02:
	unitwalk get_instance_var("guide"),194,281,instance_npcname(strnpcinfo(0)) + "::OnEvent03";
end;

OnEvent03:
	npctalk "However, since there are 3 automatic dolls managing, I can tell that this place has an important purpose.";
	unitwalk get_instance_var("guide"),196,279,instance_npcname(strnpcinfo(0)) + "::OnEvent04";
end;

OnEvent04:
	unitwalk get_instance_var("guide"),196,265,instance_npcname(strnpcinfo(0)) + "::OnEvent05";
end;

OnEvent05:
	unitwalk get_instance_var("guide"),201,265,instance_npcname(strnpcinfo(0)) + "::OnEvent06";
end;

OnEvent06:
	sleep 5000;
	npctalk "That's weird. The automatic dolls here would be dormant when nothing special happening, but they're not visible.";
	unitwalk get_instance_var("guide"),196,265,instance_npcname(strnpcinfo(0)) + "::OnEvent07";
end;

OnEvent07:
	unitwalk get_instance_var("guide"),195,276,instance_npcname(strnpcinfo(0)) + "::OnEvent";
end;

OnEvent08:
	unitwalk get_instance_var("guide"),196,265,instance_npcname(strnpcinfo(0))+"::OnEvent09";
end;

OnEvent09:
	unitwalk get_instance_var("guide"),203,265,instance_npcname(strnpcinfo(0))+"::OnEvent10";
end;

OnEvent10:
	unitwalk get_instance_var("guide"),202,264,instance_npcname(strnpcinfo(0))+"::OnEvent11";
end;

OnEvent11:
	unitwalk get_instance_var("guide"),202,252,instance_npcname(strnpcinfo(0))+"::OnEvent2";
end;
	
OnTouch:
	if(get_instance_var("hgd") == 10){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
	}
end;

OnGuardKill:
	set_instance_var("guard_count",get_instance_var("guard_count") + 1);
	if(get_instance_var("guard_count") == 3){
		sleep2 2000; 
		npctalk "Are you not a human but a combat unit, [" + strcharinfo(0) + "]?";
		sleep2 3000;
		npctalk "[" + strcharinfo(0) + "], You're strong. It takes a lot of combat power to subdue three class 4 combat-type automatic dolls at the same time.";
		set_instance_var("hgd",12);
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
	}
end;
}


1@herbs,194,282,4	script	#172_hgd_guard_0	CLEAR_NPC,{
	end;
	
OnSummon:
	npcspeed 150;
	.@id = atoi(replacestr(strnpcinfo(2),"172_hgd_guard_",""));
	switch(.@id){
		case 0: .@x = 194; .@y = 279; break;
		case 1: .@x = 194; .@y = 273; break;
		case 2: .@x = 191; .@y = 276; break;
	}
	unitwalk getnpcid(0,instance_npcname(strnpcinfo(0))),.@x,.@y,instance_npcname(strnpcinfo(0)) + "::OnSummon2";
end;

OnSummon2:
	.@id = atoi(replacestr(strnpcinfo(2),"172_hgd_guard_",""));
	switch(.@id){
		case 0: .@x = 194; .@y = 279; break;
		case 1: .@x = 194; .@y = 273; break;
		case 2: .@x = 191; .@y = 276; break;
	}
	instance_disable(strnpcinfo(0));
	monster get_instance_var("map$"),.@x,.@y,"Surveillance Doll",1739,1,instance_npcname("#172_hgd_guide_3") + "::OnGuardKill";
end;
}

1@herbs,194,270,7	duplicate(#172_hgd_guard_0)	#172_hgd_guard_1	CLEAR_NPC
1@herbs,188,276,5	duplicate(#172_hgd_guard_0)	#172_hgd_guard_2	CLEAR_NPC

1@herbs,85,101,3	script	#172_hgd_guide_4	20622,3,3,{
	if(get_instance_var("hgd") == 13){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		set_instance_var("hgd",14);
		set_instance_var("guide",getnpcid(0,instance_npcname(strnpcinfo(0))));
		npcspeed 200;
		npctalk "It's my first time here, this is where Sigma's body resides. Usually, communication is done only through the network.";
		unitwalk get_instance_var("guide"),85,91,instance_npcname(strnpcinfo(0))+"::OnEvent01";
	}
	end;
	
OnEvent01:
	unitwalk get_instance_var("guide"),85,71,instance_npcname(strnpcinfo(0))+"::OnEvent02";
end;

OnEvent02:
	unitwalk get_instance_var("guide"),85,62,instance_npcname(strnpcinfo(0))+"::OnEvent03";
	specialeffect EF_WHITEBODY,AREA,instance_npcname("#172_hgd_sigma");
	specialeffect EF_MOONSTAR,AREA,instance_npcname("#172_hgd_sigma");
end;

OnEvent03:
	npctalk "Sigma. I have brought [" + get_instance_var("admin$") + "].";
	sleep 3000;
	npctalk "Well done. You may go now.",instance_npcname("#172_hgd_sigma");
	sleep 3000;
	npctalk "I'm not finished with my guide. Is it okay if I return like this?";
	sleep 3000;
	npctalk "Oh, I'll record the subsequent information for the next guide. You can go back at ease.",instance_npcname("#172_hgd_sigma");
	sleep 3000;
	npctalk "Okay. I will follow the instructions.";
	unitwalk get_instance_var("guide"),85,72,instance_npcname(strnpcinfo(0))+"::OnEvent04";
end;

OnEvent04:
	unitwalk get_instance_var("guide"),85,92,instance_npcname(strnpcinfo(0))+"::OnEvent05";
end;

OnEvent05:
	unitwalk get_instance_var("guide"),85,99,instance_npcname(strnpcinfo(0))+"::OnEvent06";
end;

OnEvent06:
	set_instance_var("hgd",15);
	instance_disable(strnpcinfo(0));
	specialeffect EF_MARKING_USE_CHANGEMONSTER,AREA,instance_npcname("#172_hgd_sigma");
end;

OnTouch:
	if(get_instance_var("hgd") == 13){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
	}
end;
}

1@herbs,85,56,8	script	#172_hgd_sigma	4_M_BARMUND,4,4,{
	.@var = get_instance_var("hgd");
	specialeffect EF_WHITEBODY;
	specialeffect EF_MOONSTAR;
	if(.@var == 15){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		select("Varmundt? Why are you here?");
		set_instance_var("hgd",16);
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "Of course. You know the master Varmundt well. [" + strcharinfo(0) + "].";
		sleep2 5000;
		npctalk "Unfortunately, I'm not Varmundt. This appearance is something that I created because there is something that I want to check on you.";
		sleep2 3000;
		setpcblock PCBLOCK_ALL,false;
		select("Then, who are you?");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "I am one of the holographic device type AI that is designed to learn Varmundt's personality.";
		sleep2 5000;
		npctalk "Please understand if my tone is not like a machine. Varmundt was the only one who talked to me...";
		sleep2 6000;
		setnpcdisplay instance_npcname(strnpcinfo(0)),20621;
		npctalk "Now I'm going back to my original self. It's easier to maintain the original form.";
		sleep2 4000;
		npctalk "Oh. This is an early form that was called Red Pepper by Varmundt. I'm optimized for auxiliary work.";
		setpcblock PCBLOCK_ALL,false;
		sleep2 2000;
		set_instance_var("guide",getnpcid(0,instance_npcname(strnpcinfo(0))));
		npcspeed 200;
		unitwalk get_instance_var("guide"),85,75,instance_npcname(strnpcinfo(0))+"::OnEvent01";
		end;
	}
	if(.@var == 17){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		npctalk "Can you answer me? Have you met Varmundt before?";
		sleep2 3000;
		switch(select("I don't really remember.:We've met at Glastheim.:I met him trying to save the world.")){
			case 1:
				npctalk "Oh, that's possible. He might have made you forget what you saw...";
				break;
				
			case 2:
				npctalk "Was it on that place again...";
				break;
				
			case 3:
				npctalk "As expected, he was doing something important...";
				break;
		}
		sleep2 3000;
		npctalk "Anyway, it's important that the automatic dolls here reacted to you.";
		sleep2 6000;
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;	
		npctalk "Now, let's get down to business. I need your help.";
		sleep2 6000;
		npctalk "There are two more red peppers in this security area besides me, Kappa and Lambda.";
		sleep2 6000;
		npctalk "In the absence of master Varmundt for a long time, this security area is subject to the cancellation and closure of research records under the agreement of three Red Peppers.";
		sleep2 2000;
		set_instance_var("hgd",18);
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
		end;
	}
	if(.@var == 18){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		npctalk "Can you go tell them that Varmundt is alive?";
		sleep2 2000;
		select("What difference would it make?");
		setpcblock PCBLOCK_ALL,true;
		sleep2 1000;
		npctalk "They're trying to close this research area. I'm still against it, but the resource is starting to hit the limit.";
		sleep2 5000;
		npctalk "But once they find out Varmundt is alive, they won't be able to shut this place down.";
		sleep2 5000;
		specialeffect EF_WHITEBODY;
		specialeffect EF_MOONSTAR;	
		npctalk "This all happened because Kappa and Lambda were on different task.";
		sleep2 2000;
		setpcblock PCBLOCK_ALL,false;
		select("What's the difference between your tasks?");
		set_instance_var("hgd",19);
		sleep2 2000;
		npctalk "It will be better to show you that personally.";
		setpcblock PCBLOCK_ALL,false;
		unitwalk get_instance_var("guide"),83,113,instance_npcname(strnpcinfo(0))+"::OnEvent08";
		end;
	}
	if(.@var == 20){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		npctalk "All right, let's take a break. Ask me if you have any questions. I'll answer everything I can.";
		while(true){
			specialeffect EF_WHITEBODY;
			specialeffect EF_MOONSTAR;
			sleep2 2000;
			setpcblock PCBLOCK_MOVE,false;
			.@talk$ = (.@talk > 0 ? "Why are the automatic dolls farming?":"");
			.@talk2$ = (.@talk2 > 0 ? "What's your relationship with Alpha?":"");
			.@talk3$ = (.@talk3 > 0 ? "How were you born?":"");
			switch(select("Why does the flower garden exist?",.@talk$,.@talk2$,.@talk3$)){
				case 1:
					setpcblock PCBLOCK_MOVE,true;
					npctalk "This security area was not supposed to be revealed to the world. So I planned a number of ways to cover it up.";
					sleep2 5000;
					npctalk "In order to hide and conceal this place, we decided to disguise it as an extremely normal and peaceful place.";
					sleep2 5000;
					npctalk "So it became the garden around the security area. Doesn't it look like a normal garden?";
					sleep2 3000;
					.@talk = 1;
					break;
					
				case 2:
					setpcblock PCBLOCK_MOVE,true;
					npctalk "I've never asked them to farm. We just threw things away and watched how AI reacted.";
					sleep2 5000;
					npctalk "Only the ones who started farming stood out in the garden.";
					sleep2 5000;
					npctalk "I wish I could show Varmundt the process of self-learning automatic doll that can be left unattended without instructions...";
					sleep2 3000;
					.@talk2 = 2;
					break;
					
				case 3:
					setpcblock PCBLOCK_MOVE,true;
					npctalk "She has evolved from an early version of the Varmundt's management doll.";
					sleep2 5000;
					npctalk "Master Varmundt valued her emotional aesthetic so highly that he gave her the task to manage the mansion, not the research facility here.";
					sleep2 5000;
					npctalk "With the aesthetic sensibility that we don't have, she upgraded the appearance of every automatic doll that managed the mansion to look like herself.";
					sleep2 5000;
					npctalk "However, she recognizes that this security area was created according to Varmundt's personal taste.";
					sleep2 5000;
					npctalk "So she doesn't approach this place. Maybe they're taking it as some sort of sanctuary...";
					sleep2 3000;
					.@talk3 = 3;
					break;
					
				case 4:
					set_instance_var("hgd",21);
					instance_event(strnpcinfo(0),"OnEvent",false);
					setpcblock PCBLOCK_MOVE,false;
					end;
			}
		}
	}
	if(.@var == 22){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		if(isbegin_quest(12497) == 0){
			select("Who was that?");
			mes "[ Sigma ]";
			mes "Lambda. She developed intellectually and began to perceive herself as a woman.";
			next;
			mes "[ Sigma ]";
			mes "It's a big problem, she's about to make a leap here.";
			next;
			mes "[ Sigma ]";
			mes "Take this. A recognition code that could lead to other areas of this secret garden.";
			close2;
			setquest 12497;
			completequest 12497;
			getitem 1000103,20;
			specialeffect EF_MARKING_USE_CHANGEMONSTER;
		} else {
			mes "[ Sigma ]";
			mes "There's an approximately 82% chance you'll lose your life in her radical suppression before we let her know that Master Varmundt is alive.";
			next;
			mes "[ Sigma ]";
			mes "Let's avoid this place today and make a promise next time to come again. Next time you'll be free to go to another area.";
			next;
			mes "[ Sigma ]";
			mes "Hold me tight. I'll warp you out.";
			close2;
			warp "ba_maison",136,309;
		}
	}
	end;
	
OnEvent:
	specialeffect EF_WHITEBODY;
	specialeffect EF_MOONSTAR;
	.@map$ = get_instance_var("map$");
	mapannounce .@map$,"??? : You're a good talker, Sigma. Do you want to be initialized right now?",bc_map,0xFF4444;
	sleep 5000;
	npctalk "Oh... she must have been observing you too.";
	sleep 5000;
	mapannounce .@map$,"??? : It's not enough to have outsiders in the garden, but to even leak information. I can't overlook it.",bc_map,0xFF4444;
	sleep 5000;
	mapannounce .@map$,"??? : With the authority of the Red Pepper, we will warp the whole research area. I'll take responsibility after that.",bc_map,0xFF4444;
	set_instance_var("hgd",22);
	specialeffect EF_MARKING_USE_CHANGEMONSTER;
end;
	
OnEvent01:
	npctalk "This place is restricted unless you have the same pattern as Varmundt's mana. I was surprised to hear that an outsider is inside the garden.";
	unitwalk get_instance_var("guide"),73,87,instance_npcname(strnpcinfo(0))+"::OnEvent02";
end;

OnEvent02:
	npctalk "I even speculated that Varmundt might have return here after hundreds of years in disguise.";
	unitwalk get_instance_var("guide"),62,98,instance_npcname(strnpcinfo(0))+"::OnEvent03";
end;

OnEvent03:
	unitwalk get_instance_var("guide"),55,98,instance_npcname(strnpcinfo(0))+"::OnEvent04";
end;

OnEvent04:
	specialeffect EF_WHITEBODY;
	specialeffect EF_MOONSTAR;	
	sleep 1500;
	instance_event(strnpcinfo(0),"OnEffect",false);
	sleep 500;
	npctalk "Let's touch this for a moment and talk. I need to be fussed a little...";
	sleep 5000;
	npctalk "You're face is new to me. Then I realized, maybe this person met with Varmundt in some place before.";
	sleep 7000;
	npctalk "It's not just once or twice, but if you've met Varmundt over and over for quite some time, it's understandable that the automatic dolls gets mistaken.";
	sleep 6000;
	npctalk "So I kept watching you.";
	unitwalk get_instance_var("guide"),65,98,instance_npcname(strnpcinfo(0))+"::OnEvent05";
end;

OnEvent05:
	.@gid = get_instance_var("guide");
	npctalk "There's been a series of references of Varmundt in this security area, and yet you never asked who he was.";
	unitwalk get_instance_var("guide"),64,110,instance_npcname(strnpcinfo(0))+"::OnEvent06";
end;

OnEvent06:
	unitwalk get_instance_var("guide"),64,113,instance_npcname(strnpcinfo(0))+"::OnEvent07";
end;

OnEvent07:
	sleep 5000;
	npctalk "It seemed like you knew him for long time already, at least in my eyes.";
	set_instance_var("hgd",17);
	specialeffect EF_MARKING_USE_CHANGEMONSTER;
end;

OnEvent08:
	specialeffect EF_WHITEBODY;
	specialeffect EF_MOONSTAR;
	npctalk "We were originally holographic device-based research support AI. I can't even exert any physical force.";
	sleep 5000;
	npctalk "We've been working on organizing and recording the process alongside with Varmundt's research.";
	sleep 5000;
	npctalk "As data accumulates up over time, the simple electronic signal developed into what you humans often call intelligence.";
	sleep 5000;
	npctalk "At some point we began to understand his research and we were able to predict what decisions he would make.";
	sleep 5000;
	npctalk "We felt something about the fact that improving our skills was helpful to him. It must have been a feeling of joy.";
	sleep 5000;
	npctalk "And one day he gave us orders.";
	sleep 3000;
	instance_hide("#172_hgd_lambda",false);
	instance_hide("#172_hgd_kappa",false);
	instance_hide("#172_hgd_varmundt",false);
	specialeffect EF_WHITEBODY,AREA,instance_npcname("#172_hgd_varmundt");
	sleep 1000;
	npctalk "[Lambda, starting today, make sure you manage all security rights for the results of this secret lab.]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[Affirmative.]",instance_npcname("#172_hgd_lambda");
	sleep 4000;
	npctalk "[Kappa, make sure you get sufficient support on your research. As long as finances are not at risk.]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[I'll perform the instruction.]",instance_npcname("#172_hgd_kappa");
	sleep 4000;
	npctalk "[And Sigma, starting today, you're going to continue my research on artificial intelligence.]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[I don't have that ability.]";
	sleep 4000;
	specialeffect EF_WHITEBODY;
	specialeffect EF_MOONSTAR;
	specialeffect EF_WHITEBODY,AREA,instance_npcname("#172_hgd_varmundt");
	npctalk "[You'll have to do it, Sigma. I have to go on a long journey from now on.]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[Are you coming back?]";
	sleep 4000;
	npctalk "[I don't know.]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[I am a simple research assistant machine. Why are you entrusting me with such a task.]";
	sleep 4000;
	npctalk "[This is because among the three of you, you were the fastest to resemble me.]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[My calculations shows that the probability of success is less than 2%. This is an impossible mission.]";
	sleep 4000;
	specialeffect EF_WHITEBODY;
	specialeffect EF_MOONSTAR;
	specialeffect EF_WHITEBODY,AREA,instance_npcname("#172_hgd_varmundt");
	npctalk "[Well, you don't know that until you try, right?]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[I'm looking forward to your research while I'm away. I wish I could see again you someday.]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[Kappa and Lambda, assist Sigma on the research.]",instance_npcname("#172_hgd_varmundt");
	sleep 4000;
	npctalk "[Affirmative.]",instance_npcname("#172_hgd_lambda");
	npctalk "[I'll perform the instruction.]",instance_npcname("#172_hgd_kappa");
	specialeffect EF_BIG_PORTAL2,AREA,instance_npcname("#172_hgd_varmundt");
	sleep 1000;
	instance_disable("#172_hgd_varmundt");
	sleep 3000;
	npctalk "Since then, we've started using the facilities here to make automatic dolls that can learn human traits.";
	unitwalk get_instance_var("guide"),90,113,instance_npcname(strnpcinfo(0))+"::OnEvent09";
end;

OnEvent09:
	specialeffect EF_WHITEBODY;
	specialeffect EF_MOONSTAR;	
	instance_disable("#172_hgd_lambda");
	instance_disable("#172_hgd_kappa");
	unitwalk get_instance_var("guide"),107,113,instance_npcname(strnpcinfo(0))+"::OnEvent10";
end;

OnEvent10:
	npctalk "After about 300,000 hours, the accumulated knowledge reached the stage of forming a virtual personality.";
	unitwalk get_instance_var("guide"),107,94,instance_npcname(strnpcinfo(0))+"::OnEvent11";
end;


OnEvent11:
	npctalk "Since then, intelligence has risen exponentially.";
	unitwalk get_instance_var("guide"),97,84,instance_npcname(strnpcinfo(0))+"::OnEvent12";
end;

OnEvent12:
	npctalk "Since then, we've started renovating existing automatic dolls to learn about human personalities using the facilities here.";
	unitwalk get_instance_var("guide"),86,73,instance_npcname(strnpcinfo(0))+"::OnEvent13";
end;

OnEvent13:
	specialeffect EF_WHITEBODY;
	specialeffect EF_MOONSTAR;
	npctalk "The children created from this time are the children outside the garden you saw.";
	sleep 1000;
	specialeffect EF_MARKING_USE_CHANGEMONSTER;
	set_instance_var("hgd",20);
end;
	
OnTouch:
	if(get_instance_var("hgd") == 15 || get_instance_var("hgd") == 17){
		removespecialeffect EF_MARKING_USE_CHANGEMONSTER;
		specialeffect EF_MARKING_USE_CHANGEMONSTER;
	}
end;

OnEffect:
	while(get_instance_var("hgd") < 17){
		specialeffect EF_HITLINE2,AREA,instance_npcname("#172_hgd_eff_" + rand(3));
		sleep 500;
	}
	for(.@i = 0; .@i < 3; .@i++)
		instance_disable("#172_hgd_eff_" + .@i);
end;
}

1@herbs,53,97,3	duplicate(dummynpc2)	#172_hgd_eff_0	CLEAR_NPC
1@herbs,53,98,3	duplicate(dummynpc2)	#172_hgd_eff_1	CLEAR_NPC
1@herbs,53,99,3	duplicate(dummynpc2)	#172_hgd_eff_2	CLEAR_NPC

1@herbs,82,112,6	duplicate(dummynpc2)	#172_hgd_lambda	20621
1@herbs,82,114,6	duplicate(dummynpc2)	#172_hgd_kappa	20620
1@herbs,90,113,4	duplicate(dummynpc2)	#172_hgd_varmundt	4_M_BARMUND

1@herbs,45,208,0	script	#172_sec_ev_n	HIDDEN_WARP_NPC,3,3,{
	end;
	
OnTouch:
	if(!is_party_leader()) end;
	instance_disable(strnpcinfo(0));
	instance_disable("#172_sec_ev_h");
	instance_disable("#172_hgd_guide_0");
	instance_enable("#172_sec_guide",true);
	set_instance_var("guide",getnpcid(0,instance_npcname("#172_sec_guide")));
	set_instance_var("admin$",strcharinfo(0));
	set_instance_var("beta",20622);
	set_instance_var("flower",20624);
	set_instance_var("plant",20626);
	instance_enable("#172_sec_kappa",true);
	warp get_instance_var("map$"),150,80;
	unitwalk get_instance_var("guide"),53,196,instance_npcname("#172_sec_guide") + "::OnEvent01";
end;
}

1@herbs,59,214,0	script	#172_sec_ev_h	HIDDEN_WARP_NPC,3,3,{
	end;
	
OnTouch:
	if(!is_party_leader()) end;
	instance_disable(strnpcinfo(0));
	instance_disable("#172_sec_ev_n");
	instance_disable("#172_hgd_guide_0");
	instance_enable("#172_sec_guide",true);
	set_instance_var("guide",getnpcid(0,instance_npcname("#172_sec_guide")));
	set_instance_var("admin$",strcharinfo(0));
	set_instance_var("mode",1);
	set_instance_var("beta",20623);
	set_instance_var("flower",20625);
	set_instance_var("plant",20627);
	unitwalk get_instance_var("guide"),53,196,instance_npcname("#172_sec_guide") + "::OnEvent01";
end;
}

1@herbs,70,196,8	script	#172_sec_guide	20622,{
	end;
	
OnEvent01:
	npcspeed 200;
	unitwalk get_instance_var("guide"),53,207,instance_npcname(strnpcinfo(0))+"::OnStart";
end;

OnStart:
	.@name$ = get_instance_var("admin$");
	.@pepper$ = get_instance_var("pepper$");
	.@map$ = get_instance_var("map$");
	npctalk "[" + .@name$ + "]! Sigma asked me to deliver a message.";
	sleep 3000;
	npctalk "The current situation isn't good, I think it's better for you to go back...";
	sleep 3000;
	mapannounce .@map$,"Lambda : It's up to me to decide whether those outsiders go back or not.",bc_map,0xFFFF44;
	sleep 3000;
	mapannounce .@map$,"Lambda : You've created a fatal threat to the security in this area. I will dispose of it in accordance with the automatic doll management policy.",bc_map,0xFFFF44;
	specialeffect EF_THUNDERSTORM2;
	sleep 1000;
	specialeffect EF_M03;
	instance_disable(strnpcinfo(0));
	sleep 2000;
	mapannounce .@map$,"Lambda : Just in time, I want to get a confirmation from you.",bc_map,0xFFFF44;
	sleep 5000;
	mapannounce .@map$,"Lambda : It's been a while since an outsider came into this security area, so I'd better say what the other automatic dolls told me.",bc_map,0xFFFF44;
	sleep 5000;
	mapannounce .@map$,"Lambda : We need to use you to 'evaluate' our security system.",bc_map,0xFFFF44;
	instance_event("#172_sec_mob","OnSummon",false);
end;
}

1@herbs,1,1,0	script	#172_sec_mob	HIDDEN_WARP_NPC,{
	end;
	
OnSummon:
	.@beta = get_instance_var("beta");
	.@plant = get_instance_var("plant");
	.@flower = get_instance_var("flower");
	.@map$ = get_instance_var("map$");
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
	.@id = get_instance_var("sec");
	.@mode = get_instance_var("mode");
	switch(.@id){
		case 0:
			if(!.@mode){
				setarray .@xy,
				77,197,.@beta,0,51,196,.@beta,6000,63,217,.@beta,3000,81,195,.@flower,0,61,217,.@beta,3000,
				57,193,.@beta,6000,56,191,.@plant,3000,59,193,.@plant,6000,59,192,.@beta,0,60,222,.@beta,0,
				58,191,.@beta,6000,78,199,.@beta,3000,54,192,.@plant,0,79,195,.@beta,9000,76,192,.@plant,0,51,196,.@flower,1;
			} else {
				setarray .@xy,
				53,197,.@beta,3000,62,217,.@beta,3000,57,219,.@plant,0,62,222,.@beta,0,79,200,.@beta,0,80,195,.@beta,0,
				80,199,.@beta,0,58,193,.@beta,0,56,192,.@flower,3000,51,191,.@beta,0,51,192,.@beta,0,83,199,.@flower,0,83,191,.@beta,3000,
				83,196,.@beta,6000,57,193,.@beta,0,62,222,.@beta,1;
			}
			break;
			
		case 1:
			if(!.@mode){
				setarray .@xy,
				114,226,.@beta,0,114,227,.@beta,0,119,217,.@flower,6000,118,227,.@beta,3000,116,216,.@plant,6000,
				114,215,.@plant,3000,114,225,.@beta,6000,114,224,.@beta,0,139,205,.@beta,0,140,202,.@beta,0,145,202,.@plant,0,
				136,202,.@flower,0,142,208,.@flower,0,144,203,.@flower,0,170,223,.@flower,0,174,220,.@flower,0,
				168,221,.@beta,0,169,221,.@beta,0,169,217,.@beta,0,118,216,.@beta,0,117,233,.@beta,1;
			} else {
				setarray .@xy,
				137,204,.@beta,0,142,205,.@plant,0,145,200,.@beta,0,146,201,.@flower,6000,147,201,.@beta,3000,141,208,.@plant,0,
				166,224,.@flower,6000,171,217,.@plant,3000,172,225,.@plant,0,173,214,.@beta,0,174,225,.@plant,0,174,223,.@plant,6000,
				170,213,.@beta,3000,169,219,.@beta,6000,136,205,.@beta,0,136,210,.@beta,0,120,224,.@beta,0,118,223,.@beta,0,117,219,.@beta,0,
				115,214,.@beta,3000,115,233,.@beta,1;
			}
			break;
			
		case 2:
			if(!.@mode){
				setarray .@xy,
				213,205,.@beta,0,202,214,.@plant,3000,202,213,.@plant,3000,210,212,.@plant,6000,211,207,.@flower,3000,209,214,.@flower,3000,203,212,.@beta,0,218,194,.@beta,6000,203,196,.@beta,3000,
				204,193,.@plant,3000,215,201,.@flower,0,204,193,.@plant,3000,205,213,.@plant,3000,218,208,.@beta,6000,210,210,.@beta,0,245,214,.@plant,6000,220,192,.@beta,0,248,208,.@beta,0,253,208,.@beta,0,
				253,208,.@plant,6000,246,212,.@flower,6000,251,210,.@beta,3000,245,209,.@plant,0,222,195,.@flower,0,203,212,.@flower,6000,208,213,.@beta,0,219,192,.@beta,0,245,209,.@plant,1;
			} else {
				setarray .@xy,
				207,194,.@flower,0,217,204,.@beta,3000,210,210,.@plant,3000,218,195,.@beta,3000,211,209,.@plant,3000,210,211,.@flower,3000,207,194,.@flower,3000,209,214,.@beta,3000,
				216,192,.@beta,3000,208,211,.@beta,3000,214,208,.@beta,6000,209,214,.@beta,0,215,201,.@plant,0,205,214,.@beta,0,205,214,.@flower,6000,220,207,.@flower,3000,208,213,.@beta,0,
				202,212,.@beta,3000,202,193,.@beta,0,206,214,.@beta,0,245,212,.@plant,0,247,209,.@beta,0,249,210,.@beta,0,249,214,.@plant,0,249,214,.@plant,0,250,210,.@beta,0,252,208,.@plant,1;
			}
			break;
			
		case 3:
			if(!.@mode){
				setarray .@xy,
				291,210,.@plant,0,306,215,.@plant,0,284,234,.@plant,0,307,232,.@plant,0,283,238,.@plant,0,298,237,.@plant,0,282,233,.@plant,0,303,240,.@plant,0,
				296,249,.@beta,0,302,239,.@beta,0,292,217,.@beta,0,298,262,.@beta,0,297,212,.@flower,0,299,262,.@flower,0,308,216,.@flower,0,301,238,.@flower,0,285,210,.@flower,0,283,228,.@flower,0,
				294,252,.@flower,3000,295,247,.@plant,6000,294,245,.@flower,3000,281,232,.@beta,0,287,219,.@plant,0,302,239,.@plant,3000,307,217,.@beta,0,300,235,.@flower,0,295,249,.@flower,0,
				284,231,.@plant,0,297,259,.@beta,0,306,238,.@plant,0,310,215,.@flower,1;
			} else {
				setarray .@xy,
				307,239,.@beta,0,297,257,.@plant,0,284,236,.@flower,0,283,235,.@flower,0,304,240,.@plant,3000,298,246,.@flower,0,305,224,.@plant,0,310,225,.@plant,9000,284,218,.@beta,0,
				298,213,.@flower,3000,308,224,.@flower,0,290,210,.@flower,0,310,224,.@plant,3000,308,220,.@plant,0,284,211,.@flower,0,284,218,.@beta,0,282,231,.@plant,0,281,231,.@flower,0,280,226,.@flower,0,
				285,210,.@flower,6000,280,227,.@beta,0,285,234,.@plant,0,283,236,.@plant,0,284,233,.@flower,3000,284,237,.@flower,0,284,239,.@plant,0,293,246,.@plant,0,300,247,.@plant,3000,
				294,253,.@plant,0,302,240,.@flower,0,299,255,.@flower,0,295,256,.@flower,3000,310,218,.@beta,1;
			}
			break;
	}
	.@size = getarraysize(.@xy);
	set_instance_var("total",0);
	set_instance_var("count",0);
	set_instance_var("total",(.@size/4));
	for(.@i = 0; .@i < .@size; .@i += 4){
		monster .@map$,.@xy[.@i],.@xy[.@i+1],"Flower Garden Watcher",.@xy[.@i+2],1,.@event$;
		sleep (.@xy[.@i+3] == 0 ? 1 : .@xy[.@i+3]);
	}
end;

OnMobKill:
	.@count = get_instance_var("count");
	.@count += 1;
	set_instance_var("count",.@count);
	if(.@count >= get_instance_var("total"))
		instance_event(strnpcinfo(0),"OnEvent",false);
end;

OnEvent:
	.@map$ = get_instance_var("map$");
	.@id = get_instance_var("sec");
	switch(.@id){
		case 0:
			mapannounce .@map$,"Lambda :  I know for sure that our security is not very good.",bc_map,0xFFFF44;
			sleep 3000;
			mapannounce .@map$,"Lambda : [" + get_instance_var("admin$") + "], I want you to [check] the next area as well.",bc_map,0xFFFF44;
			break;
			
		case 1:
			mapannounce .@map$,"Lambda : Your movements helped me a lot. [" + get_instance_var("admin$") + "]",bc_map,0xFFFF44;
			sleep 3000;
			mapannounce .@map$,"Lambda : Then, let us  move on.",bc_map,0xFFFF44;
			break;
			
		case 2:
			mapannounce .@map$,"Lambda : Sigma might be correct.",bc_map,0xFFFF44;
			sleep 3000;
			mapannounce .@map$,"Lambda : [" + get_instance_var("admin$") + "], and your party may have really met Varmundt before.",bc_map,0xFFFF44;
			sleep 3000;
			mapannounce .@map$,"Lambda : But. Before I meet you, I need to test you one last time.",bc_map,0xFFFF44;
			break;
			
		case 3:
			if(!get_instance_var("mode")){
				mapannounce .@map$,"Kappa : Well, you're just as strong as Lambda said. [" + get_instance_var("admin$") + "]",bc_map,0xFF55FF;
				sleep 3000;
				mapannounce .@map$,"Kappa : But I don't think you're not strong enough to meet Lambda yet, so I'll show you to my room.",bc_map,0xFF55FF;
				instance_enable("#172_sec_kappa",true);
			} else {
				mapannounce .@map$,"Lambda : Great. If you're this strong, you're worth talking to.",bc_map,0xFFFF44;
				sleep 3000;
				mapannounce .@map$,"Lambda : I'll take you to my room.",bc_map,0xFFFF44;
				instance_enable("#172_sec_lambda",true);
			}
			sleep 3000;
			mapannounce .@map$,"The warp connecting the flower garden and the security area has activated.",bc_map,0x33FF33;
			instance_enable("#172_sec_warp_3",true);
			end;
	}
	sleep 3000;
	mapannounce .@map$,"The warp connecting the flower garden has activated.",bc_map,0x33FF33;
	set_instance_var("sec",.@id + 1);
	instance_enable("#172_sec_warp_" + .@id,true);
end;
}

1@herbs,80,196,0	script	#172_sec_warp_0	WARPNPC,3,3,{
	end;
	
OnTouch:
	.@id = atoi(replacestr(strnpcinfo(2),"172_sec_warp_",""));
	if(!get_instance_var("warp_" + .@id) && .@id < 3){
		set_instance_var("warp_" + .@id,1);
		instance_event("#172_sec_mob","OnSummon",false);
	}
	switch(.@id){
		case 0: .@x = 101; .@y = 233; break;
		case 1: .@x = 197; .@y = 200; break;
		case 2: .@x = 276; .@y = 213; break;
		case 3:
			if(!get_instance_var("mode")){
				.@x = 151; 
				.@y = 80;
			} else {
				.@x = 244;
				.@y = 79;
			}
			break;
	}
	warp get_instance_var("map$"),.@x,.@y;
end;
}

1@herbs,174,229,0	duplicate(#172_sec_warp_0)	#172_sec_warp_1	WARPNPC,3,3
1@herbs,254,212,0	duplicate(#172_sec_warp_0)	#172_sec_warp_2	WARPNPC,3,3
1@herbs,297,264,0	duplicate(#172_sec_warp_0)	#172_sec_warp_3	WARPNPC,3,3

1@herbs,147,80,0	script	#172_sec_warp_n	WARPNPC,3,3,{
	end;
	
OnTouch:
	.@map$ = get_instance_var("map$");
	warp .@map$,94,79;
end;
}

1@herbs,242,80,0	script	#172_sec_warp_h	WARPNPC,3,3,{
	end;
	
OnTouch:
	.@map$ = get_instance_var("map$");
	warp .@map$,94,79;
end;
}

1@herbs,168,79,3	script	#172_sec_kappa	20620,4,4,{
	if(!is_party_leader()) end;
	if(get_instance_var("sec") > 3) end;
	set_instance_var("sec",4);
	npctalk "Your party ruined 30% of the garden, it's going to cost a lot to repair it.";
	sleep 5000;
	npctalk "We'd like to believe that Master Varmundt is still alive, but that's not possible with the human life span.";
	sleep 5000;
	npctalk "Nevertheless, you have to defeat me and obtain administrative authority, if you want to prevent the closure of the research area.";
	sleep 5000;
	instance_disable(strnpcinfo(0));
	instance_event("#172_sec_boss","OnSummon",false);
	end;
}

1@herbs,275,80,3	script	#172_sec_lambda	20621,{
	if(!is_party_leader()) end;
	if(get_instance_var("sec") > 3) end;
	set_instance_var("sec",4);
	npctalk "We clearly checked which part of the flower garden should be improved. Now what's left is to negotiate.";
	sleep 5000;
	npctalk "We'd like to believe that Master Varmundt is still alive, but that's not possible with the human life span.";
	sleep 5000;
	npctalk "Nevertheless, you have to defeat me and obtain administrative authority, if you want to prevent the closure of the research area.";
	sleep 5000;
	instance_disable(strnpcinfo(0));
	instance_event("#172_sec_boss","OnSummonHard",false);
	end;
}

1@herbs,85,77,7	script	#172_sec_sigma	20621,{
	if(isbegin_quest(12500)) .@qid = 12500;
	if(isbegin_quest(12499)) .@qid = 12499;
	if(checkquest(.@qid,HUNTING) == 2){
		mes "[ Sigma ]";
		mes "You did great making them surrender, but I don't think you were granted any administrator rights.";
		next;
		mes "[ Sigma ]";
		mes "Since they will recover after some time, I think you will need to continue fighting them until Master Varmundt returns.";
		next;
		mes "[ Sigma ]";
		mes "Oh.";
		mes "I brought some old items from the past. It might help you, so take it.";
		close2;
		erasequest .@qid;
		getitem 1000103,6;
		if(.@qid == 12500){
			if(rand(1,100) < 95)
				getitem 1000104,rand(1,5);
			else
				getitem 100161,1;
		}
		end;
	}
	mes "[ Sigma ]";
	mes "I'll show you the way out. What will you do?";
	next;
	if(select("I'll look around more.:I'll leave.") == 2)
		warp "ba_maison",136,309;
	end;
}

1@herbs,1,1,0	script	#172_sec_boss	HIDDEN_WARP_NPC,{
	end;
	
OnSummon:
	monster get_instance_var("map$"),169,79,"Kappa",20620,1,instance_npcname(strnpcinfo(0)) + "::OnBossKill";
	set_instance_var("gid",$@mobid[0]);
	unitskilluseid $@mobid[0],"NPC_RELIEVE_ON",7;
	instance_event(strnpcinfo(0),"OnShield",false);
end;

OnSummonHard:
	monster get_instance_var("map$"),275,80,"Lambda",20621,1,instance_npcname(strnpcinfo(0)) + "::OnBossKill";
	set_instance_var("gid",$@mobid[0]);
	instance_event(strnpcinfo(0),"OnShield",false);
end;

OnBossKill:
	stopnpctimer;
	.@map$ = get_instance_var("map$");
	killmonster .@map$,instance_npcname(strnpcinfo(0)) + "::OnBreakShield";
	if(!get_instance_var("mode"))
		instance_enable("#172_sec_warp_n",true);
	else
		instance_enable("#172_sec_warp_h",true);
	instance_enable("#172_sec_sigma",true);
	mapannounce .@map$,"Sigma : I have activated the warp to my location. Come over here.",bc_map,0xF6D6B6;
end;

OnShield:
	.@map$ = get_instance_var("map$");
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBreakShield";
	.@gid = get_instance_var("gid");
	killmonster .@map$,.@event$;
	if(get_instance_var("mode"))
		setarray .@xy,299,95,299,64,272,64,272,95;
	else
		setarray .@xy,171,94,171,65,200,65,200,94;
	for(.@i = 0; .@i < getarraysize(.@xy); .@i += 2)
		monster .@map$,.@xy[.@i],.@xy[.@i+1],"Guardian Part",20679,1,.@event$;
	unitskilluseid get_instance_var("gid"),"NPC_RELIEVE_OFF",1;
	unitskilluseid get_instance_var("gid"),"NPC_RELIEVE_ON",10;
	mapannounce .@map$,"SYSTEM : The manager has summoned Guardian Parts and will recover them as materials for it's energy shield.",bc_map,0xFFFF00;
	sleep 40000;
	if(mobcount(.@map$,.@event$)){
		killmonster .@map$,.@event$;
		mapannounce get_instance_var("map$"),"SYSTEM : A default energy shield value has been recovered.",bc_map,0xFFFF00;
		initnpctimer;
	}
end;

OnBreakShield:
	.@map$ = get_instance_var("map$");
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBreakShield";
	.@gid = get_instance_var("gid");
	.@count = mobcount(.@map$,.@event$);
	switch(.@count){
		case 0:
			unitskilluseid get_instance_var("gid"),"NPC_RELIEVE_OFF",1;
			mapannounce .@map$,"SYSTEM : All the Guardian Parts have been destroyed. No energy shield has been recovered.",bc_map,0xFFFF00;
			initnpctimer;
			end;
			
		case 1: .@val = 3; break;
		case 2: .@val = 5; break;
		case 3: .@val = 7; break;
	}
	if(unitexists(.@gid)){
		unitskilluseid get_instance_var("gid"),"NPC_RELIEVE_OFF",1;
		unitskilluseid get_instance_var("gid"),"NPC_RELIEVE_ON",.@val;
		mapannounce .@map$,"SYSTEM : The damage to the Guardian Parts has weakened the energy shield.",bc_map,0xFFFF00;
	}
end;

OnTimer30000:
	stopnpctimer;
	.@gid = get_instance_var("gid");
	if(!unitexists(.@gid)) end;
	getunitdata .@gid,.@data;		
	if(.@data[UMOB_HP] <= ((.@data[UMOB_MAXHP]/10) * 8) && get_instance_var("summon_1") == 0){
		set_instance_var("summon_1",1);
		instance_event(strnpcinfo(0),"OnShield",false);
		end;
	}
	if(.@data[UMOB_HP] <= ((.@data[UMOB_MAXHP]/10) * 6) && get_instance_var("summon_2") == 0){
		set_instance_var("summon_2",1);
		instance_event(strnpcinfo(0),"OnShield",false);
		end;
	}
	if(.@data[UMOB_HP] <= ((.@data[UMOB_MAXHP]/10) * 4) && get_instance_var("summon_3") == 0){
		set_instance_var("summon_3",1);
		instance_event(strnpcinfo(0),"OnShield",false);
		end;
	}
	if(.@data[UMOB_HP] <= ((.@data[UMOB_MAXHP]/10) * 2) && get_instance_var("summon_4") == 0){
		set_instance_var("summon_4",1);
		instance_event(strnpcinfo(0),"OnShield",false);
		end;
	}
	initnpctimer;
end;
}